#!/usr/bin/perl

#
# NethServer Samba
#
# Create new user(s) in SAM database.
#
# The new user name is specified by the second argument to the script.
# If the user name value has not been set ALL users in AccountDB are processed.
#
#
# Copyright (C) 2012 Nethesis S.r.l.
# http://www.nethesis.it - support@nethesis.it
# 
# This script is part of NethServer.
# 
# NethServer is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License,
# or any later version.
# 
# NethServer is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
# 
# You should have received a copy of the GNU General Public License
# along with NethServer.  If not, see <http://www.gnu.org/licenses/>.
#

use strict;
use esmith::AccountsDB;
use File::Path;
use User::pwent;
use NethServer::Samba;

my $event = shift;
my $user = shift;
my @users;
my $accountsDb = esmith::AccountsDB->open();

my $exitCode = 0;

my $pdbedit = '/usr/bin/pdbedit';
my $smbpasswd = '/usr/bin/smbpasswd';
my $net = '/usr/bin/net';

if($user) {
    if($accountsDb->get_prop($user, 'type') ne 'user') {
	die("Invalid user account name: `$user`");
    }
    @users = ($user);
} else {
    @users = map { $_->key } $accountsDb->users();   
}

my $prefix = NethServer::Samba->new()->get_sam_prefix();

foreach my $user (@users) {

    my $userEnt = getpwnam($user);
    if( ! $userEnt) {
	warn "[WARNING] User $user not found";
	next;
    }

    my $userShare = '/var/lib/nethserver/home/' . $user;

    # Ensure home directory exists and set proper permissions:
    if( ! -d $userShare) {
	File::Path::make_path($userShare);
	chmod 0700, $userShare;
	chown $userEnt->uid, $userEnt->gid, $userShare;
    }

    # Check if user exists in SAM:
    qx($pdbedit -L -u "$user" >/dev/null 2>&1);
    if($? == 0) {
	next;
    }

    # Map the primary group in SAM database, before the user
    qx($net sam mapunixgroup "$user");
    if($? != 0) {
	warn "Failed to map user's unix group `$user` in SAM database";
	$exitCode ++;
    }

    # Add to SAM:
    qx($smbpasswd -a -n -d "$user" >/dev/null 2>&1);
    if($? != 0) {
	warn "Failed to add user `$user` in SAM database";
	$exitCode ++;
	next;
    }

    # Default user Samba state ( Refs #974 ):
    # active user => Samba disabled; 
    my $userState = $accountsDb->get_prop($user, '__state');
    if ($userState eq 'active') {
	$accountsDb->set_prop($user, 'Samba', 'disabled');
    }

    # Make sure the password is in expired state, to avoid valid empty passwords:
    if(system($net, qw(sam set pwdmustchangenow), "$prefix\\$user", 'yes') != 0) {
	warn "Failed to set password state of SAM user `$user` to expired";
	$exitCode ++;
    }
}

exit(($exitCode > 0) ? 1 : 0);


