#!/usr/bin/perl -w
#
# Copyright (C) 2012 Nethesis S.r.l.
# http://www.nethesis.it - support@nethesis.it
# copyright (C) 2001 Mitel Networks Corporation
# 
# This script is part of NethServer.
# 
# NethServer is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License,
# or any later version.
# 
# NethServer is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
# 
# You should have received a copy of the GNU General Public License
# along with NethServer.  If not, see <http://www.gnu.org/licenses/>.
#

#
# On unix account for samba machine see:
# http://www.samba.org/samba/docs/man/Samba-HOWTO-Collection/domain-member.html#machine-trust-accounts
#

package esmith;

use strict;
use esmith::AccountsDB;
use NethServer::Directory;
use User::grent;
use esmith::lockfile;

my $accountsDb = esmith::AccountsDB->open or die("Couldn't open accounts db\n");
my $ldap = NethServer::Directory->new();

my $event = shift;
my $machineName = shift;

# Validate account name: machine account names MUST end with dollar sign '$':
if ( $machineName !~ /\$$/ ) {
    die("Machine name $machineName is not a valid machine account name\n");
}

my $uid;
my $lock;

# Create accounts DB record, if not exist
my $machine = $accountsDb->get($machineName);
if ($machine) {
    # Check account type correctness
    if ($machine->prop('type') ne 'machine') {
	die("Account $machineName is not of type machine\n");
    }
    $uid = $machine->prop('Uid');
} else {
    # Create the machine record in AccountsDB
    $machine = $accountsDb->new_record($machineName, {type => "machine"});
}

if( ! $uid) {
    # Allocate and store new Uid in AccountsDB:
    $lock = esmith::lockfile::LockFileOrWait("/var/lib/nethserver/db/accounts"); 
    $uid = $accountsDb->get_next_uid;
    $machine->set_prop('Uid', $uid);
}

my $unixGroup = getgrnam('domcomputers') || getgrnam('nobody');

my $response = $ldap->add(sprintf("uid=%s,ou=Computers,%s", $machineName, NethServer::Directory::getInternalSuffix()), attrs => [
	       objectClass => ['account', 'posixAccount'],
	       cn => $machineName . ' Samba machine account',
	       description => $machineName . ' Samba machine account',
	       uid => $machineName,
	       uidNumber => $uid,
	       gidNumber => $unixGroup->gid,
	       homeDirectory => '/none',
	   ]);

if($response->is_error) {
    die("Failed to create machine account $machineName.\n");
}

# Release lock if we have one:
if($lock) {
    esmith::lockfile::UnlockFile($lock);
}




